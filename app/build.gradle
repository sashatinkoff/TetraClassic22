plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
    id 'androidx.navigation.safeargs.kotlin'
}
apply from: "$rootDir/versions.gradle"
def versionPropsFile = file('version.properties')

android {
    compileSdk build_versions.compile_sdk

    defaultConfig {
        applicationId "com.isidroid.b21"
        minSdk build_versions.min_sdk
        targetSdk build_versions.target_sdk

        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0
        def versionBuild = 0

        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger()
        }

        versionCode versionMajor * 100000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        setProperty("archivesBaseName", applicationId + "." + versionName)

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("ISIDROID_KEYSTORE"))
            storePassword System.getenv("ISIDROID_KEYSTORE_PASSWORD") ?: 'UNSET'
            keyAlias System.getenv("ISIDROID_KEY_ALIAS") ?: 'UNSET'
            keyPassword System.getenv("ISIDROID_KEY_PASSWORD") ?: 'UNSET'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        compose false
    }

    flavorDimensions 'app_type', 'mockable'
    productFlavors {
        stage {
            dimension = 'app_type'
            applicationIdSuffix = ".stage"
        }

        prod {
            dimension = 'app_type'
        }

        mock {
            dimension = 'mockable'
        }

        real {
            dimension = 'mockable'
        }
    }

    variantFilter { variant ->
        if (
        (name.contains("real") && name.contains("Mock"))
                || (name.contains("prod") && name.contains("Mock"))
                || (name.contains("stage") && name.contains("Mock") && name.contains("Release"))
        ) {
            setIgnore(true)
        }
    }
}

dependencies {
    // google
    implementation google.activity_ktx
    implementation google.app_compat
    implementation google.constraint
    implementation google.core_ktx
    implementation google.gson
    implementation google.hilt
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.4.1'
    implementation google.material
    implementation google.navigation_fragment
    implementation google.navigation_ui_ktx
    implementation google.prefs
    implementation 'androidx.room:room-runtime:2.4.2'
    implementation google.swipe_layout
    implementation google.work

    kapt google.hilt_compiler
    kapt 'androidx.room:room-compiler:2.4.2'

    // network
    implementation network.converter_gson
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.6'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.6'
    implementation 'com.squareup.okio:okio:3.0.0'
    implementation network.retrofit

    // firebase
    implementation firebase.analytics
    implementation firebase.crashlytics
    implementation firebase.messaging
    implementation platform(firebase.bom)

    // tools
    implementation 'com.github.bumptech.glide:glide:4.13.1'
    implementation tools.photoview
    implementation tools.timber
    kapt 'com.github.bumptech.glide:compiler:4.13.1'

    // test
    androidTestImplementation test.espresso_core
    androidTestImplementation test.junit_ext
    testImplementation test.junit
}